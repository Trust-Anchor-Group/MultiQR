<?xml version="1.0" encoding="utf-8"?>
<ServiceConfiguration xmlns="http://waher.se/Schema/ServiceConfiguration.xsd">
	<InitializationScript>
		<![CDATA[
		create 
		index MultiQr_Codes_Code
		on MultiQr_Codes
		(
			Code
		);
		]]>
	</InitializationScript>
	<StartupScript>
		<![CDATA[
		ConfigurationPage("Multi-QR","/MultiQR.md");

		Global["CreateMultiQR"]:=(
			(Info,Resource)->
			(
				MultiQrCode:=
				{
					"Code": Base64UrlEncode(RandomBytes(32)),
					"Title": Str(Info.Title),
					"Description": Str(Info.Description),
					"Master": Str(Info.Master),
					"Scheme": Str(Info.Scheme),
					"Created": NowUtc,
					"ExpiryDate": (empty(Info.ExpiryDate) ? null : DateTime(Info.ExpiryDate)),
					"CountLimit": (empty(Info.CountLimit) ? null : Number(Info.CountLimit)),
					"CountCategory": (empty(Info.CountCategory) ? null : Str(Info.CountCategory)),
					"Label1": Str(Info.Label1),
					"Text1": Str(Info.Text1),
					"Link1": Str(Info.Link1),
					"MultiMedia1": (exists(s2:=Info.MultiMedia1) and !empty(s2) ? Boolean(s2) : false),
					"Width1": (exists(s2:=Info.Width1) and !empty(s2) ? Number(Info.Width1) : null),
					"Height1": (exists(s2:=Info.Height1) and !empty(s2) ? Number(Info.Height1) : null)
				};

				k:=2;
				while 
					exists(Label:=Info["Label"+(s:=Str(k))]) and 
					exists(Text:=Info["Text"+s]) and 
					exists(Link:=Info["Link"+s]) do
				(
					MultiQrCode["Label"+s]:=Label;
					MultiQrCode["Text"+s]:=Text;
					MultiQrCode["Link"+s]:=Link;
					MultiQrCode["MultiMedia"+s]:=exists(s2:=Info["MultiMedia"+s]) and !empty(s2) ? Boolean(s2) : false;
					MultiQrCode["Width"+s]:=exists(s2:=Info["Width"+s]) and !empty(s2) ? Number(s2) : null;
					MultiQrCode["Height"+s]:=exists(s2:=Info["Height"+s]) and !empty(s2) ? Number(s2) : null;
					
					k++
				);

				insert into MultiQr_Codes object MultiQrCode;
				
				{
					"Image":Waher.IoTGateway.Gateway.GetUrl("/QR/"+UrlEncode(Waher.IoTGateway.Gateway.GetUrl(Resource+"?QR="+MultiQrCode.Code+"&Scheme="+MultiQrCode.Scheme))),
					"Page":Waher.IoTGateway.Gateway.GetUrl(Resource+"?QR="+MultiQrCode.Code+"&Scheme="+MultiQrCode.Scheme)
				}
			)
		);
		]]>
	</StartupScript>
</ServiceConfiguration>